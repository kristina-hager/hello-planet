//new for online promote
plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

//declare this as a Java project
apply plugin: 'java'

//customize properties
version = '1.1.1'
sourceCompatibility = 1.7
targetCompatibility = 1.7
jar.baseName = 'hello-planet'

//fetch any dependencies from jcenter
repositories {
    jcenter()
}

//declare dependencies
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

//declare required manifest attributes for JAR file
jar {
    manifest {
        attributes 'Implementation-Title': 'hello-planet example',
            'Implementation-Version': version,
            'Main-Class': 'com.example.HelloPlanet'
    }
}

//add tasks to create sources JAR
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    //set basename of sources jar to be same as regular jar
    baseName = jar.baseName
}

//create javadoc JAR
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //set basename of javadoc jar to be same as regular jar
    baseName = jar.baseName
}

//add javadoc and sources JAR to artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

def localReleaseDest = "/Users/kristina/localRepos"
//publish to Maven repo on local filesystem
uploadArchives {
    repositories {
        flatDir {
            dirs "file://${localReleaseDest}"
        }
    }
}

//require :build tasks such as :test,:assemble before upload archives
tasks.uploadArchives.dependsOn(build)


// new plugin for online promote
apply plugin: 'maven-publish'

publishing {
	publications {
        //name 'MyPublication' corresponds to 'publications' setting in bintray section
		MyPublication(MavenPublication) {
			from components.java
            groupId 'example.com'
            artifactId jar.baseName
            version version

            //add sources and javadoc jars to upload
            artifact(sourcesJar)
            artifact(javadocJar)
		}
	}
}

//bintray / jcenter configuration
//NOTE: I set credentials in my SHELL environment rather than here for security reasons
//  There is more than one way to achieve this security.
//use command: gradle bintrayUpload
//to upload the archives
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    //links to 'publishing' section above:
    publications = ['MyPublication']
    //package info. repo, name, licenses, vcsUrl are required
    pkg {
        repo = 'kh-examples'
        name = jar.baseName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kristina-hager/hello-planet.git'
        version {
            name = version
        }
    }
}

//again, add a dependency on :build to :bintrayUpload
tasks.bintrayUpload.dependsOn(build)
