plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = '1.1.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7


//create jar 
jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Hello Planet Jar File Example',
            'Implementation-Version': version,
            'Main-Class': 'com.example.HelloPlanet'
    }
}

//set basename of jar per usual jarfile naming conventions
jar.baseName = 'hello-planet'

//use jcenter to fetch external dependencies
repositories {
    jcenter()
}


//add dependencies
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    systemProperties 'property': 'value'
}

//add tasks to create sources jar
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    //set basename of sources jar to be same as regular jar
    baseName = jar.baseName
}

//create javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //set basename of javadoc jar to be same as regular jar
    baseName = jar.baseName
}

//add to artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

//adding maven information
publishing {
	publications {
        //name 'MyPublication' corresponds to 'publications' setting in bintray section
		MyPublication(MavenPublication) {
			from components.java
            groupId 'example.com'
            artifactId jar.baseName
            version version

            //add sources and javadoc jars to upload
            artifact(sourcesJar)
            artifact(javadocJar)
		}
	}
}

//adding bintray / jcenter configuration
//note: I prefer to set credentials in my shell env rather than hard-coding them here
//use command: 
//gradle bintrayUpload
//to upload the archives
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    //links to 'publishing' section above:
    publications = ['MyPublication']
    //package info. repo, name, licenses, vcsUrl are required
    pkg {
        repo = 'kh-examples'
        name = jar.baseName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kristina-hager/hello-planet.git'
        //in version, only name is required
        version {
            name = version
            desc = 'Hello Planet - a hello world java example'
        }
    }
}

//to publish to local filesystem (useful for testing)
//e.g.
//gradle uploadArchives
uploadArchives {
    repositories {
        flatDir {
            dirs '../repos'
        }
    }
}
